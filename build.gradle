plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.10.0'
}

repositories {
    mavenCentral()
}

allure {
    report {
        version.set('2.19.0')
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set('2.19.0') //версия интеграции фреймворка и Allure
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "com.github.javafaker:javafaker:1.0.2",
            "com.codeborne:selenide:6.10.3",
            "org.junit.jupiter:junit-jupiter:5.9.0",
            "org.slf4j:slf4j-simple:1.7.32",
            "io.qameta.allure:allure-selenide:2.19.0"
    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task one_property_test(type: Test) {
    useJUnitPlatform {
        includeTags("one_property")
    }
}

task hello_test(type: Test) {
    useJUnitPlatform {
        includeTags("hello_test")
    }
}

task simple_test(type: Test) {
    useJUnitPlatform {
        includeTags("simple")
        excludeTags("prod")
    }
}

task form_test(type: Test) {
    useJUnitPlatform {
        includeTags("form_test")
    }
}

help {
    doFirst {
        println ""
        println "************************************************************************ "
        println "***                                                                  *** "
        println "*** Welcome to https://github.com/qa-guru/demoqa-test-16!            *** "
        println "***                                                                  *** "
        println "*** ./gradlew help                                                   *** "
        println "***                                                                  *** "
        println "************************************************************************ "
        println ""
    }
}